###############################################################################
@cms_body[]
#-------------------------------------------------------------------------------
<script src="./scripts/actions.js" type="text/javascript"></script>
<link href="./styles/style.css" rel="stylesheet" type="text/css">
#-------------------------------------------------------------------------------
# Если параметры передаются
^if(def $form:id && def $form:idpage){
# Выводим заголовок страницы
	^cms.ShowPageHeader[Диагностика каталога;$cms.currentModule.path;^this.GetClassInfo[]]
	<p class="cms_h2">
		<a href="../?id=$idblock&idpage=$form:idpage&parent=$form:parent^if(def $form:page){&page=$form:page}"><i class="fa fa-reply fa-3x inline blue mr10"></i>К каталогу</a>
	</p>
	$params[
		$.block_id($form:id)
		$.limitCount(-1)
	]
	$elements[^this.GetElements[$params]]
# Открытые элементы
	$opens[^hash::create[]]
	$widthRedirect[^elements.select($elements.redirect ne '')]
	^widthRedirect.menu{
		$opens[^opens.union[^GetParents[$widthRedirect.id;$elements]]]
	}
	$lostElements[^hash::create[]]
# Пробегаем все элементы каталога и ищём те, родителей которых не существует
	^elements.foreach[pos;row]{
# Все элементы, у которых parent = 0 пропускаем, так как это корневые элементы и они всегда существуют
		^if($row.parent>0){
# Если parent не найден, то записываем этот элемент (и всех его потомков) в хэш
			^if(!^elements.locate[id;$row.parent]){
				$lostElements.[$row.id][$row.parent]
			}
		}
	}
# Получаем список потерянных родителей
	^if(def $lostElements){
		$lostParents[^hash::create[]]
		^lostElements.foreach[lostID;lostParent]{
			^if(!^lostParents.contains[$lostParent]){
				$lostParents.[$lostParent](1)
			}{
				^lostParents.[$lostParent].inc[]
			}
# По пути добавляем ID потерянных элементов в список открытых (если хотя бы 1 их потомок открыт)
			^if(^widthRedirect.locate($widthRedirect.parent==$lostParent)){
				^if(!^opens.contains[$lostParent]){$opens.[$lostParent](0)}
			}
		}
	}
	<div class="lost-elements">
	^if(def $lostElements){
		<div class="cms_h1">Потеряные элементы:</div>
		<div class="description">(При синхронизации или некорректной работе с каталогом могут появиться такие элементы, у которых потеряна связь с другими элементами. Такие элементы считаются "потерянными".)</div>
		<div class="elements-table">
		^lostParents.foreach[key;value]{
			$parents[^elements.select($elements.parent eq $key)]
			$parentIDs[]
			^parents.menu{
				$parentIDs[${parentIDs},${parents.id}]
			}
			$parentIDs[^parentIDs.trim[left;,]]
			<ul class="element-string^if(!^opens.contains[$key]){ close}" data-id="$key">
				<li>
					<span class="element-name-wrapper">
						<i class="fa fa-chevron-up blue"></i><span class="element-name">Неизвестный $this.elementTypes.1.title с ID=$key</span>
					</span>
				</li><li>
					<div class="redirectElement ^if(^opens.contains[$key]){green}{blue}" data-width="640" data-href="redirect.html?element_id=$parentIDs" data-title="Настроить перенаправление для $this.elementTypes.0.titleRE" data-reload="true" title="Настроить перенаправление для $this.elementTypes.0.titleRE" onClick="^$(this).OpenFancybox()^;"></div><!--
					--><div class="linkElement blue" data-width="640" data-href="link.html?element_id=$parentIDs&default=$key" data-title="Привязать $this.elementTypes.[$elements.type].title к существующему $this.elementTypes.0.titleDE" data-reload="true" title="Привязать $this.elementTypes.[$elements.type].title к существующему $this.elementTypes.0.titleDE" onClick="^$(this).OpenFancybox()^;"></div><!--
					--><div class="deleteElement red" title="Удалить $this.elementTypes.0.title и все его дочерние элементы"></div>
				</li>
				^ShowProducts[$elements;$key;;$opens]
			</ul>
		}
		</div>
	}{
		<div class="cms_h1">Потерянных элементов не найдено!</div>
	}
	</div>
}
################################################################################
@ShowProducts[allModules;parent;counter;opens][modules;moduleChilds]
^if(!def $counter){$counter(1)}
$modules[^allModules.select($allModules.parent eq $parent)]
^if(def $modules){
	^modules.menu{
		$moduleChilds[^allModules.select($allModules.parent == $modules.id)]
		$isEmpty(^modules.head.length[]<1 && ^modules.content.length[]<1)
		<ul class="element-string^if($moduleChilds && !^opens.contains[$modules.id]){ close}" data-id="$modules.id">
			<li>
				<span style="margin-left: ^eval($counter*20)px^;" class="element-name-wrapper">
					^if($moduleChilds){<i class="fa fa-chevron-up blue"></i>}<span class="element-name">$modules.name</span>
				</span>
			</li><li><!--
				^if($modules.type){--><div class="elementFullness blue^if($isEmpty){ empty}{ full}" title="^if($isEmpty){Пустой}{Заполненный} $this.elementTypes.[$modules.type].title"></div><!--}
				--><div class="redirectElement ^if(def $modules.redirect){green}{blue}" data-width="640" data-href="redirect.html?element_id=$modules.id" data-title="Настроить перенаправление для $this.elementTypes.0.titleRE" data-reload="true" title="Настроить перенаправление для $this.elementTypes.0.titleRE" onClick="^$(this).OpenFancybox()^;"></div><!--
				--><div class="linkElement blue" data-width="640" data-href="link.html?element_id=$modules.id&default=$modules.parent" data-title="Привязать $this.elementTypes.[$modules.type].title к существующему разделу" data-reload="true" title="Привязать $this.elementTypes.[$modules.type].title к существующему разделу" onClick="^$(this).OpenFancybox()^;"></div><!--
				--><div class="deleteElement red" title="Удалить $this.elementTypes.[$modules.type].title^if($moduleChilds){ и все его дочерние элементы}"></div>
			</li>
			^if($moduleChilds){^ShowProducts[$allModules;$modules.id;^eval($counter+1);$opens]}
		</ul>
	}
}
################################################################################
@GetParents[id;elements]
$parents[^hash::create[]]
$current[^elements.select($elements.id==$id)]
^while($current.parent > 0){
	$parent[^elements.select($elements.id==$current.parent)]
	^if($parent.id > 0 && !^parents.contains[$parent.id]){
		$parents.[$parent.id]($current.id)
	}
	$current[$parent]
}
$result[$parents]
################################################################################